plugins {
    id 'jacoco'
}

def customJacocoReportDir = 'jacoco/reports'

jacoco {
    toolVersion = '0.8.11'
}

jacocoTestReport {
    reports {
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.dir("$customJacocoReportDir/html"))

        xml.required.set(true)
        xml.outputLocation.set(layout.buildDirectory.file("$customJacocoReportDir/xml/jacoco.xml"))

        csv.required.set(true)
        csv.outputLocation.set(layout.buildDirectory.file("$customJacocoReportDir/csv/jacoco.csv"))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

dependencies {
    // spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // jwt
    implementation 'com.auth0:java-jwt:3.18.2'

    // test
    implementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation project(':infrastructures:infra-user-mysql')
}

tasks.named('test') {
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
    useJUnitPlatform()
}