plugins {
    id 'jacoco'
}

def customJacocoReportDir = 'jacoco/reports'

jacoco {
    toolVersion = '0.8.11'
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required.set(true)
        html.outputLocation.set(layout.buildDirectory.dir("$customJacocoReportDir/html"))

        xml.required.set(true)
        xml.outputLocation.set(layout.buildDirectory.file("$customJacocoReportDir/xml/jacoco.xml"))

        csv.required.set(true)
        csv.outputLocation.set(layout.buildDirectory.file("$customJacocoReportDir/csv/jacoco.csv"))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.7
            }
        }
    }
}

dependencies {
    implementation project(':domains:domain-common')
    implementation project(':domains:domain-user')

    // spring
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // jwt
    implementation 'com.auth0:java-jwt:3.18.2'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'com.h2database:h2'

}


tasks.register('loadDotEnvFile') {
    if (file('.env').exists()) {
        file('.env').eachLine { line ->
            def (key, value) = line.split('=')
            System.setProperty(key, value)
        }
    }
}

tasks.named('test') {
    dependsOn loadDotEnvFile
    finalizedBy jacocoTestReport, jacocoTestCoverageVerification
    useJUnitPlatform()
    systemProperties System.getProperties()
}